apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.neonex.lbs"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 6
        versionName "1.1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(SIGNED_STORE_FILE)   // gradle.properties 파일 참조 앱 빌드키
            storePassword SIGNED_STORE_PASSWORD // gradle.properties 파일 참조 앱 빌드키
            keyAlias SIGNED_KEY_ALIAS           // gradle.properties 파일 참조 앱 빌드키
            keyPassword SIGNED_KEY_PASSWORD     // gradle.properties 파일 참조 앱 빌드키
            v1SigningEnabled true               // 앱빌드 방식 필수 값
            v2SigningEnabled true               // 앱빌드 방식 필수 값
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release    // 빌드 키 이용
            minifyEnabled false                     // 소스 축소
            zipAlignEnabled true                    // 스토어 올리기위한 zip 정렬

            //하단 부터 Build Variant에서 빌드선택후 빌드 하면 BuildConfig.Java파일에 변수가 생성됨.
            buildConfigField "boolean", "IS_DEV",  "false"
            buildConfigField "boolean", "IS_TEST",  "false"
            buildConfigField "boolean", "IS_MDM_USE", "true"
            buildConfigField "String",  "WEB_URL",  "\"https://ods.ibkc.co.kr\""
            buildConfigField "String",  "APPIRON_URL", "\"https://apprn.ibkc.co.kr/authCheck.call\""
        }

        ttest {
            signingConfig signingConfigs.release

            //하단 부터 Build Variant에서 빌드선택후 빌드 하면 BuildConfig.Java파일에 변수가 생성됨
            buildConfigField "boolean", "IS_DEV", "false"
            buildConfigField "boolean", "IS_TEST", "true"
            buildConfigField "boolean", "IS_MDM_USE", "true"
            buildConfigField "String", "WEB_URL", "\"https://odst.ibkc.co.kr:444\""
            buildConfigField "String", "APPIRON_URL", "\"https://apprnt.ibkc.co.kr:444/authCheck.call\""
        }

        debug{
            signingConfig signingConfigs.release

            //하단 부터 Build Variant에서 빌드선택후 빌드 하면 BuildConfig.Java파일에 변수가 생성됨
            buildConfigField "boolean", "IS_DEV",  "true"
            buildConfigField "boolean", "IS_TEST",  "false"
            buildConfigField "boolean", "IS_MDM_USE", "false"
            buildConfigField "String",  "WEB_URL",  "\"https://odsd.ibkc.co.kr:8080\""
            buildConfigField "String",  "APPIRON_URL", "\"https://apprnd.ibkc.co.kr:8080/authCheck.call\""
        }
    }

    //여기서 생성된 앱의 이름을 바꿔준다
    def formattedDate = new Date().format('yyyyMMdd_HHmm')
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "skb_lbs_${variant.name}_${variant.versionName}_${formattedDate}.apk"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'gun0912.ted:tedpermission:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.firebase:firebase-messaging:10.2.6'
    compile 'com.google.firebase:firebase-core:10.2.6'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup:otto:1.3.8'
}

apply plugin: 'com.google.gms.google-services'
